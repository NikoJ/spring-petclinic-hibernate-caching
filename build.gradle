import io.qameta.allure.gradle.task.AllureReport

buildscript {
    apply from: "$rootDir/gradle/buildscriptVersions.gradle"

    ext {
        testCoverageFile = "$buildDir/jacoco/coverage.exec"
    }

    repositories {
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}")
        classpath "com.netflix.nebula:gradle-lint-plugin:${gradleLintVersion}"
        classpath "com.github.lkishalmi.gradle:gradle-bom-plugin:${gradleBomVersion}"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarqubeVersion}"
        classpath "io.qameta.allure:allure-gradle:2.5"
    }
}

apply plugin: 'org.sonarqube'
apply plugin: 'com.github.lkishalmi.bill-of-materials'
apply from: "$rootDir/gradle/versions.gradle"

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'nebula.lint'

    jacoco {
        toolVersion = "${jacocoVersion}"
    }

    gradleLint {
        rules = ['unused-dependency']
        alwaysRun = false
        reportFormat = 'html'
    }

    group = 'org.springframework.samples.petclinic'
    version = '1.6.0-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.qameta.allure'

    allure {
        version = '2.8.1'
        downloadLinkFormat = 'https://dl.bintray.com/qameta/maven/io/qameta/allure/allure-commandline/%s/allure-commandline-%<s.zip'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    dependencies {
        compile 'org.projectlombok:lombok'
        compile 'p6spy:p6spy:3.7.0'
        compile 'org.slf4j:slf4j-api'
        testCompile 'io.qameta.allure:allure-junit4:2.9.0'
        testCompile 'io.qameta.allure:allure-selenide:2.9.0'
        testCompile "io.qameta.allure:allure-gradle:2.5"
        testCompile 'ru.yandex.qatools.allure:allure-junit-adaptor:1.5.4'

        annotationProcessor 'org.projectlombok:lombok'
    }

    task allureAggregatedReport(type: AllureReport) {
        resultsDirs = subprojects.allure.resultsDir
    }

    sonarqube {
        properties {
            property "sonar.jacoco.reportPaths", testCoverageFile
        }
    }
}

jar {
    enabled = false
}

sonarqube {
    properties {
        property "sonar.host.url", "http://localhost:9001"
        property "sonar.projectKey", "sonar:petclinic"
        property "sonar.projectName", "petclinic"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.verbose", "true"
        property "sonar.exclusions", "**/build/generated/**"
        property "sonar.coverage.exclusions", "**/test/*, **/*Entity.java, **/*Dto.java, **/*View.java, **/*Exception.java, **/*Event.java, **/data/**, **/model/**, **/exception/**, **/error/**, **/config/**, **/properties/**, **/meta/**"
    }
}

task jacocoMergeTest(type: JacocoMerge) {
    destinationFile = file(testCoverageFile)
    executionData = project.fileTree(dir: '.', include: '**/build/jacoco/*.exec')
}

tasks['sonarqube'].with {
    dependsOn.clear()
    dependsOn(jacocoMergeTest)
}
